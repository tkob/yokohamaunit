*[Pair]: yokohama.unit.util.Pair

# Test: Zipping empty lists results in a empty list

Assert that `Pair.zip(fs, ss)` is `[]` where fs = `[]` and ss = `[]`.

# Test: First is bigger than second

Assert `Pair.zip([1], [])` throws an instance of `IllegalArgumentException`.

# Test: Second is bigger than first

Assert `Pair.zip([], [1])` throws an instance of `IllegalArgumentException`.

# Test: Non empty lists

Assert `Pair.zip([1], [2])` is `[new Pair(1, 2)]`.

Assert `Pair.zip([1, 2], [3, 4])` is `[new Pair(1, 3), new Pair(2, 4)]`.

# Test: Unzipping an empty list results in empty lists

Assert that `Pair.unzip([])` is `new Pair([], [])`.

# Test: Unzipping non empty lists

Assert `Pair.unzip(list)` is `new Pair(firsts, seconds)`
where list is `[new Pair(1, "a"), new Pair(2, "b")]`
and firsts is `[1, 2]` and seconds is `["a", "b"]`.
